---
layout: post
title: bash命令之awk学习笔记
categories: bash
---

### awk命令

awk是一种编程语言，用于在linux/unix下对文本和数据进行处理。数据可以是来自标准输入(stdin)、一个或多个文件，或其他命令的输出。它支持用户自定义函数和动态正则表达式等先进功能，是linux/unix下的一个强大编程工具。它在命令行中使用，但更多是作为脚本来使用。awk有很多内建的功能，比如数组、函数等，这是它和c语言的相同之处，灵活性是awk的最大优势。



#### awk命令格式和选项

---

**语法形式**

> awk [options] 'scripte' var=value file(s)
>
> awk [options] -f scriptefile var=value file(s

**常用命令选项**

* `-F` fs fs指定输入分隔符，fs可以字符串或则正则表达是，如`-F:`
* `-v` var=value 赋值一个用户定义变量，将外部变量传递给awk
* `-f` scripfile 从脚本文件中读取awk命令
* `-m[fr]` val duival值设置内在限制，-mf选项限制分配给val的最大快数目；-mr选项限制记录的最大数目。这两个功能是Bell是实验室版awk的扩展功能，在标准awk中不适用



#### awk模式和操作

---

awk脚本是由模式和操作组成

**模式**

模式可以是一下任意一个:

* /正则表达式/: 使用通配符的扩展集
* 关系表达式：使用运算符进行操作，可以是字符串或数字的比较测试
* 模式匹配表达式：用运算符`~`(匹配)和`~!`(不匹配)
* BEGIN语句块、pattern语句块、END语句块

**操作**

操作由一个或多个命令、函数、表达式组成，之间由换行符或分号隔开，并位于大括号内，主要部分是：

* 变量或数组赋值
* 输出命令
* 内置函数
* 控制流语句


#### awk脚本基本结构

---

> awk 'BEGIN{print "start"} pattern{ commands } END{print "end"}' file

一个awk脚本通常由:BEGIN语句块、能够使用模式匹配的通用语句块、END语句块3部分组成，这三个部分是可选的。任意一个部分都可以不出现在脚本中，脚本通常是被单引号或双引号中，例如：

```shell
awk 'BEGIN{ i=0 } { i++ } END{ print i }' filename
awk "BEGIN{ i=0 } { i++ } END{ print i }" filename
```

**awk的工作原理**

```shell
awk 'BEGIN{ commands } pattern{ commands } END{ commands }'
```

* 第一步: 执行`BEGIN{ commands }`语句块中的语句;
* 第二步: 从文件或标准输入(stdin)读取一行, 然后执行`pattern{ commands }`语句块, 它逐行扫描文件, 从第一行到最后一行重复这个过程, 直到文件被读取完毕;
* 第三步: 当读至输入流末尾时, 执行`END{ commands }`语句块;

***BEGIN语句块***在awk开始从输入流中读取行之前被执行,这是一个可选的语句块, 比如变量初始化、打印输出表格的表头等语句通常可以写在BEGIN语句块中.

***END语句块***在awk从输入流中读取完所有执行之后即被执行, 比如打印所有行的分析结果这类信息汇总都是在END语句块中完成, 它也是一个可选语句块.

***pattern语句块****中的通用命令是最重要的部分,他也是可选的.如果没有提供pattern语句块, 则默认执行`{ print }`, 即打印每一个读取到的行, awk 读取的每一行都会执行该语句块.

**示例**

```shell
echo -e "1 line \n2 line" | awk 'BEGIN{ print "start"} { print } END{ print "end" }'
```

当使用不带参数的`print`时, 它就打印当前行, 当`print`的参数是以`逗号进行分割时,打印这是空格`作为界定符. awk的print语句块中双引号是被当做链接符使用,例如:

```shell
echo | awk '{ var1="v1"; var2="v2"; var2="v3"; print var1, var2, var3; }'
```

双引号拼接使用: 

```shell
echo | awk '{ var1="v1"; var2="v2"; var3="v3"; print var1"="var2"="var3; }'
```

{ }类似一个循环体, 会对文件中的每一行进行迭代,通常变量初始化语句( 如: i=0 )以及打印文件头部的语句放入BEGIN语句块中, 将打印的结果等语句放在END语句块中.

#### awk内置变量(预定义变量)

***说明***: `[A][N][P][G]`表示第一个支持变量的工具, [A]=awk, [N]=nawk, [P]=POSIXawk, [G]=gawk

> **$n** 当前记录的第n个字段, 比如n为1表示第一个字段, n为2表示第二个字段;
>
> **$0** 这个变量包含执行过程中当前行的文本内容;
>
> [N] **ARGC** 命令行参数的数目;
>
> [G] **ARGIND** 命令行中当前文件的位置(从0开始算);
>
> [N] **ARGV** 包含命令行参数的数组;
>
> [G] **CONVFMT** 数字转换格式(默认值为%.6g);
>
> [P] **ENVIRON** 环境变量关联数组;
>
> [N] **ERRNO** 最后一个系统错误的描述;
>
> [G] **FIELDWIDTHS** 字段宽度列表(用空格键分隔);
>
> [A] **FILENAME** 当前输入文件的名;
>
> [P] **FNR** 同NR, 但相对于当前文件;
>
> [A] **FS** 字段分隔符(默认是任何空格);
>
> [G] **IGNORECASE** 如果为真, 则进行忽略大小写的匹配;
>
> [A] **NF** 表示字段数, 在执行过程中对应于当前的字段数;
>
> [A] **NR** 表示记录数, 在执行过程中对应于当前的行号
>
> [A] **OFMT** 数字的输出格式(默认值是%0.6g);
>
> [A] **OFS** 输出字段分隔符(默认值是一个空格);
>
> [A] **ORS** 输出记录分隔符(默认值是一个换行符);
>
> [RS] **RS** 记录分隔符(默认一个换行符);
>
> [N] **RSTART** 由match函数所匹配的字符串的第一个位置;
>
> [N] **RLENGTH** 由match函数所匹配的字符串长度;
>
> [N] **SUBSEP** 数组下标分隔符(默认值是34);

**示例**

```shell
echo -e "line1 f2 f3\nline2 f4 f5\nline3 f6 f7" | awk '{ print "Line No:"NR", No of fields:"NF, "$0="$0, "$1="$1, "$2="$2, "$3="$3}'
```

使用`$NF`可以打印出一行中的最后一个字段，使用`$(NF-1)`则是打印倒数第二个字段，其他以此类推:

```shell
echo -e "line1 f2 f3\n line2 f4 f5" | awk '{print $NF}'
echo -e "line1 f2 f3\n line2 f4 f5" | awk '{print $(NF-1)}'
```

打印每一行的第二和第三个字段:

```shell
awk '{print $2, $3}' filename
```

统计文件中的行数:

```shell
awk 'END{ print NR }' filename
```

以上命令只是使用了END语句块， 在读入每一行的时，awk会将NR更新为对应的行号，当到达最后一行NR值就是最后一行的行号，所以END语句块中的NR就是文件的行数。

一个每一行中第一个字段值累加的例子:

```shell
seq 5 | awk 'BEGIN{sum=0;} {sum+=$1} END{print "sum="sum}'
```

#### 将外部变量值传递给awk

---

借助`-v`**选项**, 可以将外部值(并非来自stdin)传递给awk:

```shell
var=10000
echo | awk -v value=$var '{print value}'
```

另外一种传递外部变量的方法:

```shell
var1="aaa"
var2="bbb"
echo | awk '{print v1, v2}' v1=$var1 v2=$var2
```

当输入来自于文件时使用:

```shell
awk '{ print v1, v2 }' v1=$var1 v2=$var2 filename
```

以上方法中， 变量之间用空格分隔作为awk的命令行参数跟随在BEGIN、{}和END语句块之后。

#### awk运算与判断

---

作为一种程序设计语言所应具有的特点之一，awk支持多种运算，这些运算与c语言提供的基本相同。awk还提供了一系列内置的运算函数(如log、sqr、cos、sin等)和一些用于对字符串进行操作(运算)的函数(如length、substr等等 )。这些函数的应用大大的提高了awk的运算功能。作为对条件转移指令的一部分，关系判断是没中程序设计语言都具备的功能，awk也不例外，awk中允许进行多种测试，作为样式匹配，还提供了模式匹配表达式~(匹配)和~!(不匹配)。作为对测试的一种扩充，awk也支持用逻辑运算符。

**算术运算符**

| 运算符    | 描述             |
| ------ | -------------- |
| + -    | 加， 减           |
| * /  & | 乘， 除与求余        |
| + - !  | 一元加， 减和逻辑非     |
| ^ \*** | 求幂             |
| ++ --  | 增加或减少， 作为前缀或后缀 |

例：

```shell
awk 'BEGIN{a="b"; print a++, ++a;}'
```

> 0 2

**注意**：所有用作算术运算符进行操作，操作数自动转为数值，所有非数值都变为0

**赋值运算符**

| 运算符                       | 描述   |
| ------------------------- | ---- |
| = += -= *= /= %= ^=  *\*= | 赋值语句 |

例:

```shell
a+=5;//等价于
a=a+5;//其他同类
```

**逻辑运算符**

| 运算符  | 描述   |
| ---- | ---- |
| \|\| | 逻辑或  |
| &&   | 逻辑与  |

例:

```shell
awk 'BEGIN{ a=1; b=2; print (a > 5 && b <= 4), (a > 5 || b <= 4);}'
```

**正则运算符**

| 运算符  | 描述               |
| ---- | ---------------- |
| ~ ~! | 匹配正则表达式和不匹配正则表达式 |

例:

```shell
awk 'BEGIN{ a="100test1";if(a ~ /^100*/) {print"ok";} }'
```

**关系运算符**

| 运算符             | 描述    |
| --------------- | ----- |
| < <= > >= != == | 关系运算符 |

```shell
awk 'BEGIN{ a=11; if(a >= 9) {print "ok";}}'
```

**注意:** > < 可以作为字符串比较也可以

**其他运算符**

| 运算符  | 描述         |
| ---- | ---------- |
| $    | 字段引用       |
| 空格   | 字符串连接符     |
| ?:   | c条件表达式     |
| in   | 数组中是否存在某键值 |

例:

```shell
awk 'BEGIN{a="b";print a=="b"?"ok":"err";}'
```

```shell
awk 'BEGIN{a="b";arr[0]="b";arr[1]="c";print (a in arr);}'
```

> 0

```shell
awk 'BEGIN{a="b";arr[0]="b";arr["b"]="c";print(a in arr);}'
```

> 1

**运算符优先级表**

| 级别   | 运算符                                      | 说明                 |
| ---- | ---------------------------------------- | ------------------ |
| 1    | =, +=, -=, *=, /=, %=, &=, ^=, \|=, <<=, >>= | 赋值, 运算, 赋值         |
| 2    | \|\|                                     | 逻辑或                |
| 3    | &&                                       | 逻辑与                |
| 4    | \|                                       | 按位或                |
| 5    | ^                                        | 按位异或               |
| 6    | &                                        | 按位与                |
| 7    | ==, !=                                   | 等于, 不等于            |
| 8    | <=, >=, <, >                             | 小于等于, 大于等于, 小于, 等于 |
| 9    | <<, >>                                   | 按位左移, 按位右移         |
| 10   | +, -                                     | 加, 减               |
| 11   | *, /, %                                  | 乘, 除, 取模           |
| 12   | !, ~                                     | 逻辑非, 按位取反或补码       |
| 13   | -, +                                     | 正, 负               |

> 级别越高越优先

#### awk高级输入输出

---

**读取下一条记录**

awk中`next`语句使用: 在循环逐行匹配, 如果遇到next, 就会跳过当前行, 直接忽略下面语句. 而进行下一行匹配. net语句一般用于多行合并:

```shell
echo -e "a\nb\nc\nd\ne" | awk 'NR%2==1{next}{print NR, $0}'
```

当记录行号除以2余1, 就跳过当前航. 下面的`print NR, $0`也不会执行. 下一行开始, 程序又开始判断`NR%2`的值. 这个时候记录是`2`, 就会执行打印语句块

分析发现需要将包含有"web"进行跳过, 然后需要将内容与下面行合并为一行:

```shell
str="web01[192.168.56.100]\nhttpd\ntomcat\nsendmail\nweb02[192.168.56.101]\nmysqld\npostfix";
echo -e $str | awk '/^web/{T=$0;next;}{print T":\t"$0;}'
```

**简单地读取一条记录**

`awk getline`用法: 输出重定向需用到`getline函数`. getline从标准输入, 管道或者当前正在处理的文件之外的其他输入文件获得. 它负责从输入获得下一行的内容, 并给NF, NR和FNR等内建变量赋值. 如果得到一条记录, getline函数返回1, 如果达到文件的末尾就返回0, 如果出现错误, 例如打开文件失败, 就返回-1.

getline语法: getline var, 变量包含了特定行的内容.

awk getline从整体上来说, 用法说明:

* **当其左右无重定向符`|`或`<`时:** getline作用于当前文件, 读入当前文件的第一行给其后跟的变量`var`或`$0`(无变量), 应该注意到, 由于awk在处理getline之前已经读入了一行, 所以getline得到的返回结果是隔行的.
* **当其左右有重定向符`|`或`<`时:** getline则作用于定向输入文件, 由于该文件是刚打开, 并没有被awk读入一行, 只是getline读入, 那么getline返回的是该文件的第一行, 而不是隔行.

**示例:** 

执行linux的`date`命令, 并通过管道输出给`getline`, 然后在把输出赋值给自定义变量out, 并打印它:

```shell
awk 'BEGIN{"date" | getline out; print out}'
```

执行shell的`date`命令, 并通过管道输出给`getline`, 然后再把输出赋值给自定义变量out, 并打印它:

```shell
awk 'BEGIN{"date" | getline out; split(out, mon); print mon[2]}'
```

命令ls的输出传递给getline作为输入, 循环使getline从ls的输出中读取一行, 并把它打印到屏幕. 这里没有输入文件, 因为BEGIN块在打开输入文件前执行, 所以可以忽略输入文件.

```shell
awk 'BEGIN{ while("ls" | getline) print}'
```

**关闭文件**

awk中允许在程序中关闭一个输入或输出文件, 方法是使用awk的close语句.

```shell
close("filename")
```

filename可以是getline打开的文件, 也可以是stdin, 包含文件名的变量或者getline使用的确切命令。或一个输出文件，可以是stdout, 包含文件名的变量或使用管道的确切命令.

**输出到一个文件**

```shell
echo | awk '{printf("hello word!") > "datafile"}'
#或
echo | awk '{printf("hello word!") >> "datafile"}'
```

#### 设置字段定界符

---

默认的字段定界符是空格, 可以用`-F "定界符"`明确指定一个定界符:

```shell
awk -F: '{print $NF}' /etc/passwd
#或
awk 'BEGIN{FS=":"}{print $NF}' /etc/passwd
```

在`BEGIN语句块`中则可以用`OFS="定界符"`设置输出字段的定界符.

#### 流程控制语句

---

在linux awk的while, do-while和for语句中允许使用break, continue语句来控制流程走向, 也允许使用exit这样的语句来退出. break中断当前正在执行的循环并跳到循环外执行下一条语句. if 是流程选择用法. awk中, 流程控制语句, 语法结构, 与c语言类型. 有了这些语句, 其实很多shell程序都可以交给awk, 而且性能是非常快的. 下面是各个语句用法:

**条件判断语句**

```c
/**
 * if(表达式)
 * 	语句1
 * else
 * 	语句2
 */
```

格式中语句1可以是多个语句, 为了方便判断和阅读, 最好将多个语句用{}括起来. awk分支结构允许嵌套.

实例:

```shell
awk 'BEGIN{
 test=100;
 if(test > 90){
 	print "very good";
 }
 else if(test > 60){
 	print "good";
 }
 else{
 	print "no pass";
 }
}'
```

**循环语句**

`while语句` 

示例:

```shell
awk 'BEGIN{
  test=100;
  total=0;
  while(i <= test){
  	total += i;
  	i++;
  }
  print total;
}'
```

`for循环`

```shell
#格式1
awk 'BEGIN{
  for(k in ENVIRON){
  	print k"="ENVIRON[k];
  }
}'
#格式2
awk 'BEGIN{
  total=0;
  for(i=0;i<=100;i++){
  	total+=i;
  }
  print total;
}'
```

`do循环`

```shell
awk 'BEGIN{
  total=0;
  i=0;
  do {total+=i;i++} while(i <= 100)
  print total;
}'
```

**其他语句**

* **break** 当break语句用于while或for语句时, 导致退出程序循环.
* **continue** 当continue语句用于while或for语句时, 使程序循环移动到下一个迭代.
* **next** 能够导致读入下一个输入行, 并返回到脚本的顶部. 这可以避免对当前输入执行其他操作过程.
* **exit** 语句使主输入循环退出并将控制转移到END, 如果END存在的话. 如果没有定义END规则, 或在END中应用exit语句, 则终止脚本的执行.

#### 数组应用

---

数组是awk的灵魂, 处理文本中最不能少的就是它的数组处理.因为数组索引(下标)可以是数字和字符串在awk中数组叫做关联数组(associative arrays). awk中的数组不必提前声明, 也不必声明大小.数组元素用0或空字符串来初始化, 这根据上下文而定.

**数组的定义**

数字做数组索引(下标):

```shell
Array[1]="sun"
Array[2]="kai"
```

字符串做数组索引(下表):

```shell
Array["first"]="www"
Array["last"]="name"
Array["birth"]="1987"
```

使用中`print Array[1]`会打印出sun;使用`print Array[2]`会打印出kai;使用`print Array["birth"]`会得到1987

**读取数组的值**

```shell
{for(item in array)}{print array[item];} #输出的顺序是随机的
{for(i1;i<=len;i++)}{print array[i];}   #len是数组的长度
```

**得到数组长度**

```shell
awk 'BEGIN{info="it is a test";lens=split(info,array, " ");}{print length(array), lens;}'
```

length返回字符串以及数组长度, spilt进行分割字符串为数组, 也会返回分割得到数组长度.

```shell
awk 'BEGIN{info="it is a test";split(info,array, " ");}{print length(array), lens;}'
```

asort对数组进行排序, 返回数组长度

**输出数组内容(无序, 有序输出):**

```shell
awk 'BEGIN{info="it is a test";split(info, arr, " "); for(k in arr) {print k, arr[k];}}'
#不一定输出都是无序的
```

`for...in`输出, 因为数组是关联数组, 默认是无序的.所以通过`for...in`得到的是无序的数组. 如果需要得到有序数组, 下标获得.

```shell
awk 'BEGIN{info="it is a test";tlen=split(info, arr, " "); for(k = 1; k<=tlen;k++) {print k, arr[k];}}'
```

**注意:** 数组下表是从1开始, 与c数组不一样

**判断键值是否存在及删除键值**

```shell
#错误的判断方法
awk 'BEGIN{arr["a"]="a1";arr["b"]="b1";if(arr["c"] != "1"){print "no found";}for(k in arr){print k, arr[k];}}'
```

以上出现奇怪问题, `arr[c]`没有定义, 但是循环时候, 发现已经存在该键值, 它的值为空, 这里需要注意, awk数组是关联数组, 只要通过数组引用它的key, 就会自动创建该序列

```shell
#正确判断方法
awk 'BEGIN{arr["a"]="a";arr["b"]="b";if("c" in arr){print "ok";}for(k in arr){print k, arr[k];}}'
```

`if (key in array)`通过这种方式判断数组中是否包含`key`键值

```shell
#删除键值
awk 'BEGIN{arr["a"]="a";arr["b"]="b";delete arr["a"];for(k in arr){print k, arr[k];}}'
```

`delete array[key]`可以删除, 对应数组`key`的序列值

**二维,多维数组使用**

awk的多维数组在本质上是一维数组, 更确切一点, awk在存储上并不支持多维数组. awk提供了逻辑上模拟二维数组的访问方式. 例如, `array[2, 4] = 1`这样的访问方式时允许的. awk使用一个特殊的字符串`SUBSEP(�34)`作为分割字段, 在上面的例子中, 关联数组array存储的键值实际上是2�344.

类似一维数组的成员测试, 多维数组可以使用`if( (i,j) in array)`这样的语法, 但是下标必须放置在圆括号中. 类似一维数组的循环访问, 多维数组使用`for (item in array)`这样的语法遍历数组. 与数组唯一不同的是, 多维数组必须使用`split()`函数来访问单独的下标分量

```shell
awk 'BEGIN{
  for(i = 1; i < 10; i++){
  	for(j = 1; j < 10; j++)}{
  		arr[i, j] = i * j; print i,"*", j, "=", arr[i, j];
	}
  }
}'
```

可以通过`array[k1, k2]`引用获得数组内容

另一种方法:

```shell
awk 'BEGIN{
  for(i = 1; i < 10; i++){
  	for(j = 1; j < 10; j++)}{
  		arr[i, j] = i * j;
	}
  }
  for(m in arr){  
  	split(m, arr1, SUBSEP);print arr1[1], "*", arr1[2], "=", arr[m];
  }
}'
```

#### 内置函数

---

awk内置函数, 主要分以下3种类似: 算数函数, 字符串函数, 其他一般函数, 时间函数.

**算术函数**

| 格式            | 描述                                       |
| ------------- | ---------------------------------------- |
| atan2(y, x)   | 返回y/x的反正切                                |
| cos(x)        | 返回x的余弦; x是弧度                             |
| sin(x)        | 返回x的正弦; x是弧度                             |
| exp(x)        | 返回x幂函数                                   |
| log(x)        | 返回x的自然对数                                 |
| sqrt(x)       | 返回x平方根                                   |
| int(x)        | 返回x的截断至整数的值                              |
| rand()        | 返回任意数字n, 其中0 <= n < 1                    |
| srand([expr]) | 将rand函数的种子值设置为Expr参数的值, 或如果省略Expr参数则使用某天的时间. 返回先前的种子值 |

举例说明:

```shell
awk 'BEGIN{OFMT="%.3f";fs=sin(1);fe=exp(10);fl=log(10);fi=int(3.1415);print fs, fe, fl, fi;}'
```

OFMT设置输出数据格式是保留3位小数

获得随机数:

```shell
awk 'BEGIN{srand();fr=int(100*rand());print fr;}'
```

**字符串函数**

| 格式                              | 描述                                       |
| ------------------------------- | ---------------------------------------- |
| gsub(Ere, Repl, [In])           | 除了正则表达式所有具体值被替代这点, 它和sub函数完全一样滴执行        |
| sub(Ere, Repl, [In])            | 用repl参数指定的字符串替换In参数指定的字符串中的由Ere参数指定的扩展正则表达式的第一个具体值. sub函数返回替换的数量.出现在repl参数指定的字符串中的&(和符号)由In参数指定的与Ere参数的指定的扩展正则表达式匹配的字符串替换. 如果未指定in参数, 缺省值是整个记录($0 记录变量) |
| index(String1, String2)         | 在由String1参数指定的字符串(其中有出现String2指定的参数)中, 返回位置, 从1开始编号. 如果String2参数不在String1参数中出现, 则返回0. |
| length[(String)]                | 返回String参数指定的字符串的长度(一字节为单位).如果未给出出参数, 则返回整个记录的长度($0 记录变量) |
| blength[(String)]               | 返回String参数指定的字符串的长度(以字节为单位). 如果未给出String参数, 则返回整个记录的长度($0变量) |
| substr(string, M, [N])          | 返回具有N参数指定的字符数量子串. 子串从string参数指定的字符串取得, 其字符以M参数指定的位置开始. M参数指定为将string参数中的第一个字符作为编号1. 如果为指定N参数, 则子串的长度将是M参数指定的位置到string参数的末尾的长度. |
| match(string, Ere)              | 在string参数指定的字符串(Ere参数指定的扩展正则表达式出现在其中)中返回位置(字符形式), 从1开始编号, 或如果Ere参数不出现, 则返回0. RSTART特殊变量设置为返回值.RLENGTHT特殊变量设置为匹配字符串的长度, 或如果为找到任何匹配, 则设置为-1 |
| split(string, A, [Ere])         | 将string参数指定的参数分割为数组元素A[1], A[2], ....A[n], 并返回n变量的值.此分割可以通过Ere参数指定的扩展正则表达式进行, 或用当前字段分割符(FS 特殊变量)来进行(如果没有给出Ere参数). 除非上下文指明特定的元素还应具有一个数字值, 否则A数组中的元素用字符串值来创建 |
| tolower(String)                 | 返回string参数指定的字符串, 字符串中每个大写字符将更改为小写. 大写和小写的映射由当前语言换将的LC_CTYPE范畴定义 |
| toupper(string)                 | 返回string参数指定的字符串, 字符串中每个小写字符将更改为大写. 大写和小写的映射由当前语言环境的LC_CTYPE范畴定义. |
| sprintf(format, expr, expr,...) | 根据format参数指定的printf子例程格式字符串来格式化Expr参数指定的表达式并返回最后生成的字符串. |

**注:** Ere都可以是正则表达式

**gsub, sub使用**

```shell
awk 'BEGIN{info="this is a test201232test!";gsub(/[0-9]+/, "!", info);print info}'
```

在info中查找满足正则表达式, `/[0-9]+/`用`""`替换, 并且替换后的值, 赋值给info, 未给出info, 默认是`$0`

**查找字符串(index使用)**

```shell
awk 'BEGIN{info="this is a test"; print index(info, "test")? "ok" : "no found"}'
```

**正则表达式匹配查找(match使用)**

```shell
awk 'BEGIN{info="this is a test";print match(info, /[0-9]+/)?"ok":"no found";}'
```

**截取字符串(substr使用)**

```shell
awk 'BEGIN{info="this is a test";print substr(info, 4, 10);}'
```

**格式化字符串输出(sprintf使用)**

[格式化字符串格式]()

其中格式化字符串包括两部分内容: 一部分是正常字符, 这些字符将被按原样输出; 另一部分是格式化规定字符, 以`"%"`开始, 后跟一个或几个规定字符, 用来确定输出内容格式.

| 格式   | 描述              |
| ---- | --------------- |
| %d   | 十进制有符号整数        |
| %u   | 十进制无符号整数        |
| %f   | 浮点数             |
| %s   | 字符串             |
| %c   | 单个字符            |
| %p   | 指针的值            |
| %e   | 指数形式的浮点数        |
| %x   | %X无符号以十六进制表示的整数 |
| %o   | 无符号以八进制表示的整数    |
| %g   | 自动选择何时的表示法      |

```shell
awk 'BEGIN{n1=123.113;n2=-1.224;n3=1.2345;printf("%.2f, %.2u, %.2g, %X, %o\n", n1, n2, n3, n1, n1)}'
```

| 格式                               | 描述                                       |
| -------------------------------- | ---------------------------------------- |
| close(Expression)                | 用同一个带字符串值得Expression参数来关闭由print或printf语句打开的或调用getline函数打开的文件或管道. 如果文件或管道成功关闭, 则返回0; 其他情况下返回非零值. 如果打算写一个文件, 并稍后在同一个程序中读取文件, 则close语句时必需的 |
| system(command)                  | 执行command参数指定的命令, 并返回退出状态. 等同于system子例程  |
| Expression \| getline [Variable] | 从来自Expression参数指定的命令的输出中通过管道传送的流中读取一个输入记录, 并将该记录的值指定给Variable参数指定的的变量. 如果当前未打开将Expression参数的值作为其命令名称的流, 则创建流.创建的流等同于popen子例程, 此时command参数去Expression参数的值且Mode参数设置为一个是r的值. 只要流保留打开并且Expression参数求的同一个字符串, 则对getline函数的每次后续调用读取另一个记录. 如果未指定Variable参数, 则$0记录变量和NF特殊变量设置为从流读取的记录. |
| getline [Variable]<Expression    | 从Expression参数指定的文件读取输入的下一个记录, 并将Variable参数指定的变量设置为该记录的值. 只要流保留打开且Expression参数对同一个字符串求值, 则对getline函数的每次后续调用读取另一个记录. 如果未指定Variable参数, 则$0记录变量和NF特殊变量设置为从流读取的记录. |
| getline [Variable]               | 将Variable参数指定的变量设置为从当前输入文件读取的下一个输入记录. 如果未指定Variable参数, 则$0记录变量设置为该记录的值, 还将设置NF, NR和FNR特殊变量 |

**打开外部文件**

```shell
awk 'BEGIN{while("cat /etc/passwd" | getline){print $0;};close("/etc/passwd")}'
```

**逐行读取外部文件(getline使用方法)**

```shell
awk 'BEGIN{while(getline < "/etc/passwd"){print $0}close("/etc/passwd");}'
```

```shell
awk 'BEGIN{print "enter your name:"; getline name; print name;}'
```

**调用外部应用程序(system使用方法)**

```shell
awk 'BEGIN{b=system("ls -al");print b;}'
```

b返回值是执行结果